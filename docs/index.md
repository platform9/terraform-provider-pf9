---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "Terraform Provider for Pf9 PMK"
subcategory: ""
description: |-
  The Platform9 PMK terraform provider offers a streamlined solution for creating and managing kubernetes clusters.
---

# Pf9 Provider

Platform9 Managed Kubernetes (PMK) is a SaaS managed Kubernetes offering. Head over to the [Platform9 documentation](https://platform9.com/docs/kubernetes) to learn more about PMK.

The Pf9 Provider is a solution for creating and managing multiple clusters, attaching and detaching nodes, and managing the lifecycle of the clusters.

## Prerequisites

Before you begin, ensure you have the following:

1. Platform9 PMK account credentials (username, password and Account URL).
2. Nodes connected to your Platform9 management control plane.

## Example Usage

```terraform
terraform {
  required_providers {
    pf9 = {
      source = "platform9/pf9"
    }
  }
}

# Set the variable value in *.tfvars file
# or using -var="account_url=..." CLI option
variable "account_url" {}
variable "username" {}
variable "password" {}

# Configure the Platform9 provider
provider "pf9" {
  account_url = var.account_url
  username    = var.username
  password    = var.password
}

# Create a cluster
resource "pf9_cluster" "example" {
  name         = "example-cluster"
  master_nodes = ["<node-id>"]
  # other attributes...
}
```

## Create your first Cluster

```terraform
resource "pf9_cluster" "example" {
  name = "example"
  master_nodes = [
    "17f9b392-67bb-43b9-b0b7-3b5821f683a6",
    "7f5aa992-0abe-40a0-9bf9-6a06ebb9ccfd",
    "a17fa56d-722b-4f10-8b50-ffa5a4bed36e"
  ]
  allow_workloads_on_master = false
  worker_nodes = [
    "2bfbc40e-1d72-4bfc-a46b-56b674862cc7",
    "bbbd1c20-3cda-405d-ae4b-d0337fffd6e1"
  ]
  master_vip_ipv4              = "x.x.x.x"
  master_vip_iface             = "ens3"
  containers_cidr              = "10.20.0.0/16"
  services_cidr                = "10.21.0.0/16"
  interface_detection_method   = "InterfaceName"
  interface_name               = "ens3"
  network_plugin               = "calico"
  calico_ipv4_detection_method = "interface=ens3"
  etcd_backup = {
    daily = {
      backup_time = "02:00"
    }
  }
  tags = {
    "key1" = "value1"
  }
  addons = {
    "coredns" = {
      enabled = true
      params = {
        "CoresPerReplica"              = "257"
        "MaxReplicas"                  = "101"
        "MinReplicas"                  = "2"
        "NodesPerReplica"              = "17"
        "PollPeriodSecs"               = "301"
        "dnsDomain"                    = "cluster.local"
        "dnsMemoryLimit"               = "170Mi"
      }
      version = "1.11.1"
    },
    "kubernetes-dashboard" = {
      enabled = true
      params  = {}
    },
    "metallb" = {
      enabled = false
    },
    "metrics-server" = {
      enabled = true
      params = {
        "metricsCpuLimit"    = "100m"
        "metricsMemoryLimit" = "300Mi"
      }
      version = "0.6.4"
    }
  }
}
```

It is also possible to provide the node IDs using `nodes` data source.

```terraform
variable "master_ip" {
  description = "The primary IP address of the master node"
  type        = string
}

variable "worker1_hostname" {
  description = "The hostname of the worker node"
  type        = string
}

# find node with hostname worker1
data "pf9_nodes" "workers" {
  filter = {
    name   = "name"
    values = [var.worker1_hostname]
  }
}

# find node with primary ip address
data "pf9_nodes" "master" {
  filter = {
    name   = "primary_ip"
    values = [var.master_ip]
  }
}

# Retrieve the interface name associated with the master IP
data "pf9_host" "master" {
  id = data.pf9_nodes.master.nodes[0].id
}

locals {
  iface_name = try(
    [for iface in data.pf9_host.master.interfaces : iface.name if iface.ip == var.master_ip][0],
    null
  )
}

resource "pf9_cluster" "example" {
  name                         = "example"
  master_nodes                 = toset(data.pf9_nodes.master.nodes[*].id)
  worker_nodes                 = toset(data.pf9_nodes.workers.nodes[*].id)
  allow_workloads_on_master    = false
  master_vip_ipv4              = data.pf9_nodes.workers.nodes[0].primary_ip
  master_vip_iface             = local.iface_name
  containers_cidr              = "10.20.0.0/16"
  services_cidr                = "10.21.0.0/16"
  interface_detection_method   = "InterfaceName"
  interface_name               = local.iface_name
  network_plugin               = "calico"
  calico_ipv4_detection_method = "interface=${local.iface_name}"
  etcd_backup = {
    daily = {
      backup_time = "02:00"
    }
  }
  tags = {
    "key1" = "value1"
  }
  depends_on = [data.pf9_host.master, data.pf9_nodes.workers]
}
```

### Modify the Configuration

With the provider, you can modify the configuration of the cluster. For example, you can add or remove nodes, change the network configuration, or modify the tags.